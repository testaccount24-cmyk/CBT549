UNITS TITLE 'UNITNAME - EDT DISPLAY'
***********************************************************************
*                                                                     *
*   PROGRAM NAME. UNITNAME.                                           *
*   LOAD MODULE.  UNITNAME.                                           *
*   AUTHOR.       MARK S. BARON                                       *
*   DATE WRITTEN. DECEMBER 8, 1988.                                   *
*   DATE REVISED. OCTOBER 29, 2002 TO USE EDTINFO IN ORDER TO         *
*                 ACCOUNT FOR DYNAMIC UCB'S AND UCB'S ABOVE THE       *
*                 16 MEG LINE.                                        *
*                 ALSO, REMOVED THE DEVTYPE BYTES FROM THE DISPLAY    *
*   COMMENTS.     THIS PROGRAM DETERMINES THE UNITNAMES CREATED IN    *
*                 THE MVSCP GEN AND SET UP IN THE ELIGIBLE DEVICE     *
*                 TABLE (EDT).  THE PROGRAM USES THE UNIT             *
*                 VERIFICATION ROUTINE, IEFEB4UV, AND THE EDTINFO     *
*                 SERVICE.                                            *
*   INPUT.        PARM FIELD: NO PARM, OR PARM=NAMES.                 *
*   OUTPUT.       OUTPUT IS WRITTEN TO DDNAME UNITSOUT.               *
*                 IF NO PARM IS PASSED, THE UNITNAME AND ALL DEVICE   *
*                 NUMBERS ASSOCIATED WITH THAT UNITNAME ARE           *
*                 IF PARM=NAMES IS SPECIFIED, ONLY THE UNITNAMES      *
*                 WILL BE RETURNED.                                   *
*   DEPENDENCIES. OS/390 OR HIGHER                                    *
*                                                                     *
***********************************************************************
 TITLE 'UNITNAME - EDT DISPLAY - ENTRY, HOUSEKEEPING AND MAINLINE'
UNITNAME CSECT ,
UNITNAME AMODE 31
         SPACE ,
***********************************************************************
*                                                                     *
*   THIS IS THE MAINLINE PROCESSING FOR THE PROGRAM.  IN THE MAIN     *
*   ROUTINE, UNIT VERIFICATION SERVICES IS INVOKED TO GET THE UNIT    *
*   NAMES CORRESPONDING TO EACH DEVICE CLASS.  THE CORRESPONDING      *
*   DEVICE ADDRESSES ARE OBTAINED FOR EACH UNIT NAME IN THE LIST.     *
*                                                                     *
***********************************************************************
         SPACE ,
         SAVE  (14,12),,UNITNAME.&SYSDATE-&SYSTIME
         LR    R12,R15
         USING UNITNAME,R12
         LA    R15,SAVEAREA
         ST    R15,8(R13)
         ST    R13,4(R15)
         LR    R13,R15
         LR    R3,R1            SAVE POINTER TO PARM FIELD.
         BAS   R11,INIT         PERFORM INITIALIZATION.
         LTR   R15,R15          INITIALIZATION SUCCESSFUL?
         BNZ   RETURN           BRANCH IF NOT.
         B     LOOPUNM
LOOPUNM  DS    0H
         BAS   R11,GETUNITN     GET UNIT NAMES FROM EDT FOR DEV. CLASS.
         BNZ   RETURN           BRANCH IF NOT.
         L     R3,UNMLIST       POINT TO UNITNAME LIST.
         USING NAMELS12,R3
         MVC   UNMNUMUN,LST12NUM SAVE NUMBER OF UNITNAMES.
         LA    R3,LST12NAM      POINT TO FIRST UNITNAME IN LIST.
LOOPUNIT DS    0H
         ICM   R2,15,UNMNUMUN   LOAD NUMBER OF UNITNAMES.
         BZ    FREEUNM          BRANCH IF UNITNAMES FINISHED.
         BAS   R11,GETADDRS     GET ADDRESSES CORRESPONDING TO UNITNAME
         LA    R3,L'LST12NAM(R3) POINT TO NEXT NAME IN UNITNAME LIST.
         L     R2,UNMNUMUN      LOAD NUMBER OF UNITNAMES.
         BCTR  R2,0             SUBTRACT 1 FROM NUMBER OF UNITNAMES.
         ST    R2,UNMNUMUN
         B     LOOPUNIT
FREEUNM  DS    0H
         L     R3,UNMLIST       POINT TO UNITNAME LIST.
         L     R0,LST12HDR      LOAD SUBPOOL AND LENGTH OF UNITNM LIST
         LR    R1,R3            LOAD ADDRESS OF UNITNAME LIST.
         FREEMAIN R,LV=(0),A=(1) FREEMAIN UNITNAME LIST.
         ICM   R2,1,UNMDEVCL    GET CURRENT DEVICE CLASS MASK.
         BNZ   LOOPUNM          BRANCH IF NOT FINISHED.
         BAS   R11,CLEANUP      PERFORM TERMINATION.
RETURN   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=(15)
         DROP  R3
 TITLE 'UNITNAME - EDT DISPLAY - INITIALIZATION'
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE PERFORMS INITIALIZATION.  FILES ARE OPENED HERE,  *
*   AND THE PARM FIELD INFORMATION IS INTERPRETED.                    *
*                                                                     *
***********************************************************************
         SPACE ,
INIT     DS    0H
         LOAD  EPLOC=UVSNM      LOAD UNIT VERIFICATION ROUTINE.
         ST    R0,UVSENT        SAVE ENTRY POINT.
         OPEN  (UNITSOUT,OUTPUT),MODE=31 OPEN OUTPUT FILE.
         LR    R10,R15          TEST SUCCESSFUL OPEN.
         LTR   R15,R10
         BZ    CKPARM           BRANCH IF YES.
         WTO   'UNITNAME: OPEN FOR UNITSOUT FILE FAILED',ROUTCDE=11
         LA    R15,16
         B     INITRET
CKPARM   DS    0H
         L     R3,0(R3)         LOAD POINTER TO PARM FIELD.
         LH    R2,0(R3)         LOAD LENGTH OF PARM FIELD.
         LTR   R2,R2            CHECK FOR NULL PARM.
         BZ    INITOK           BRANCH IF NULL.
         CH    R2,=H'5'         CHECK IF LENGTH OF PARM = 5.
         BNE   BADPARM          BRANCH IF NOT.
         CLC   2(5,R3),=C'NAMES' TEST FOR PARM=NAMES.
         BNE   BADPARM          BRANCH IF NOT.
         MVI   UNMOPTSW,1       SET SWITCH FOR PARM = NAMES.
         B     INITOK
BADPARM  DS    0H
         LA    R15,8            SET BAD RETURN.
         B     INITRET
INITOK   DS    0H
         L     R1,DEVLIST       STORAGE REQUEST FOR DEVICE LIST
         GETMAIN R,LV=(1)       GET THE STORAGE
         ST    R1,NAMELSTP      SAVE THE ADDRESS
         MVC   0(4,R1),DEVLIST  MOVE THE DESCRIPTOR TO THE AREA
         LA    R2,4095          SET UP NUMBER
         LA    R2,1(,R2)          OF ENTRIES
         SLL   R2,4                  IN LIST (4096*16)
         ST    R2,4(,R1)        SAVE TO LIST
         SR    R15,R15          SET GOOD RETURN.
INITRET  DS    0H
         BR    R11              RETURN.
 TITLE 'UNITNAME - EDT DISPLAY - UNIT VERIFICATION SERVICES INTERFACE'
***********************************************************************
*                                                                     *
*   THIS ROUTINE OBTAINS ALL THE UNITNAMES FROM THE ELIGIBLE DEVICE   *
*   TABLE (EDT) FOR THE SPECIFIED DEVICE CLASS.  FOR EACH CALL TO     *
*   UNIT VERIFICATION SERVICES, EXACTLY ONE BIT IS TURNED ON IN THE   *
*   DEVICE CLASS FIELD.  AFTER EACH CALL, THIS BIT IS SHIFTED ONE     *
*   BIT TO THE LEFT, TO ENSURE THAT ALL UNIT NAMES ARE OBTAINED.      *
*                                                                     *
***********************************************************************
         SPACE ,
GETUNITN DS    0H
         XC    UVSUNTTB,UVSUNTTB ZERO OUT UNIT VERIF. UNIT TABLE.
         XC    UVSFLAGS,UVSFLAGS ZERO OUT UNIT VERIF. FLAGS FIELD.
         LA    R3,UVSUNTTB      POINT TO UNIT TABLE.
         USING UNITTAB,R3
         MVC   UNDEVCL,UNMDEVCL SET DEVICE CLASS IN UNIT TABLE.
         LA    R4,UVSFLAGS      POINT TO FLAGS FIELD.
         USING FUNCFLGS,R4
         OI    FUNCFLG2,RETNNAME SET FLAG - RETURN UNIT NAMES.
         LA    R1,UVSPARM       POINT TO UNIT VERIF. PARAMETER LIST.
         L     R15,UVSENT       LOAD UNIT VERIF. ENTRY POINT.
         BASSM R14,R15          CALL UNIT VERIFICATION SERVICES.
         LTR   R15,R15          TEST UNITNAMES OBTAINED SUCCESSFULLY.
         BNZ   GETURET          BRANCH IF NOT.
         MVC   UNMLIST,UNUCBL5P SAVE POINTER TO UNITNAMES LIST.
         SR    R2,R2
         IC    R2,UNMDEVCL      LOAD CURRENT DEVICE CLASS MASK.
         BCTR  R2,R0            CREATE NEXT MASK.
         STC   R2,UNMDEVCL      SAVE NEXT MASK.
         SR    R15,R15          SET GOOD RETURN.
GETURET  DS    0H
         BR    R11              RETURN.
         DROP  R3,R4
 TITLE 'UNITNAME - EDT DISPLAY - EDTINFO INTERFACE'
***********************************************************************
*                                                                     *
*   THIS ROUTINE OBTAINS THE DEVICE ADDRESSES ASSOCIATED WITH EACH    *
*   SYSTEM UNIT NAME.  ON ENTRY TO THIS ROUTINE, REG. 3 POINTS TO     *
*   THE NEXT UNIT NAME TO BE PROCESSED.                               *
*                                                                     *
***********************************************************************
         SPACE ,
GETADDRS DS    0H
         MVC   UNMUNAME,0(R3)   MOVE UNIT NAME TO OUTPUT LINE.
         LA    R2,NAMELSTP      POINT TO ADDRESS OF ANSWER AREA
         EDTINFO RTNDEVN,DYNAMIC=YES,LOC=ANY,RANGE=ALL,                +
               UNITNAME=(3),                                           +
               DEVNLIST=(2)
         L     R6,NAMELSTP      POINT TO THE ANSWER AREA
         LA    R7,8(,R6)        POINT TO FIRST UCB ADDRESS
         L     R6,4(,R6)        LOAD NUMBER OF UCB ADDRESSES
         LTR   R6,R6            DID WE GET ANY
         BNP   DONEADDR         NOPE - DONE WITH THIS ONE
         LA    R8,UNMUADDS      POINT TO FIRST ADDRESS ON OUTPUT LINE.
         CLI   UNMOPTSW,1       ONLY NAMES WANTED?
         BE    LASTLINE         BRANCH IF YES.
LOOPUCBA DS    0H
         MVC   0(4,R8),0(R7)    MOVE DEVICE ADDRESS TO OUTPUT LINE.
         LA    R8,L'UNMUADDS(R8) POINT TO NEXT ADDRESS ON OUTPUT LINE.
         C     R8,=A(UNMUADDE)  TEST FOR END OF LINE.
         BL    NEXTUCB          BRANCH IF NOT.
         BAS   R10,WRITLINE     WRITE OUTPUT LINE.
         LA    R8,UNMUADDS      POINT TO FIRST ADDRESS ON OUTPUT LINE.
NEXTUCB  DS    0H
         LA    R7,4(,R7)        POINT TO NEXT DEVICE NUMBER
         BCT   R6,LOOPUCBA      ONCE MORE WITH FEELING
LASTLINE DS    0H
         BAS   R10,WRITLINE     WRITE OUTPUT LINE.
         C     R8,=A(UNMUADDS)  TEST IF WE NEED ANOTHER LINKE
         BE    DONEADDR         NOPE
         BAS   R10,WRITLINE     YUP - WRITE IT OUT
DONEADDR DS    0H
         SR    R15,R15          SET GOOD RETURN IN REG. 15.
         BR    R11              RETURN.
 TITLE 'UNITNAME - EDT DISPLAY - OUTPUT SERVICE'
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE PRINTS AN OUTPUT LINE.  THE ROUTINE SWITCHES      *
*   TO 24-BIT ADDRESSING MODE, SINCE NON-VSAM I/O REQUIRES 24-BIT     *
*   CALLERS.                                                          *
*                                                                     *
***********************************************************************
         SPACE ,
WRITLINE DS    0H
         LA    R15,PUT
         SR    R2,R2
         BSM   R2,R15           SWITCH TO 24-BIT MODE.
PUT      DS    0H
         PUT   UNITSOUT,UNMOUT  WRITE OUTPUT LINE.
         LA    R15,BLNK
         OR    R15,R2
         BSM   0,R15            SWITCH TO ORIGINAL ADDRESSING MODE.
BLNK     DS    0H
         MVI   UNMOUT,C' '      BLANK OUT OUTPUT LINE.
         MVC   UNMOUT+1(L'UNMOUT-1),UNMOUT
         BR    R10
 TITLE 'UNITNAME - EDT DISPLAY - TERMINATION AND CLEANUP'
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE PERFORMS CLEANUP AT TERMINATION OF THE PROGRAM.   *
*                                                                     *
***********************************************************************
         SPACE ,
CLEANUP  DS    0H
         CLOSE UNITSOUT         CLOSE OUTPUT FILE.
         DELETE EPLOC=UVSNM     DELETE UNIT VERIFICATION ROUTINE.
         L     R0,DEVLIST       GETMAIN REQUEST
         L     R1,NAMELSTP      ADDRESS
         FREEMAIN R,LV=(0),A=(1) FREE THE STORAGE
         BR    R11              RETURN.
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - CONSTANTS'
***********************************************************************
*                                                                     *
*        WORK VARIABLES.                                              *
*                                                                     *
***********************************************************************
         SPACE ,
SAVEAREA DC    18F'0'           SAVE AREA USED BY THIS PROGRAM.
UNMLIST  DC    A(0)             POINTER TO RETURNED UNITNAME LIST.
UNMNUMUN DC    F'0'             NUMBER OF UNITNAMES IN LIST.
UNMDEVCL DC    X'FF'            DEVICE CLASS MASK.
UNMOPTSW DC    X'0'             0=UNIT ADDRESSES, 1=NAMES ONLY.
         DC    0F'0'
UNOUTDEV DC    F'0'
UNMOUT   DS    0CL80            OUTPUT LINE.
         DC    C' '
UNMUNAME DC    CL8' '           UNIT NAME.
         DC    C' '
UNMUADDS DC    14CL5' '
UNMUADDE DC    C'    '
         SPACE ,
UVSNM    DC    CL8'IEFEB4UV'    NAME OF UNIT VERIFICATION ROUTINE.
UVSENT   DC    A(0)             POINTER TO UNIT VERIFICATION ENTRY PT.
UVSPARM  DS    0F               PARAMETER LIST FOR UNIT VERIF. ROUTINE.
         DC    A(UVSUNTTB)      POINTER TO UNIT VERIF. UNIT TABLE.
         DC    A(UVSFLAGS)      POINTER TO UNIT VERIF. FLAGS.
UVSUNTTB DC    XL((UNDEVCL-UNITTAB)+6)'0' UNIT VERIF. - UNIT TABLE.
UVSFLAGS DC    XL2'0'           UNIT VERIF. - FLAGS FIELD.
NAMELSTP DC    A(DEVLIST)
DEVLIST  DC    AL1(0),AL3(65536*4+8)
         EJECT ,
UNITSOUT DCB   DSORG=PS,DDNAME=UNITSOUT,MACRF=PM,RECFM=FB,LRECL=80,    *
               BLKSIZE=7440
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - CONSTANTS - LITERAL POOL'
         LTORG ,
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - CONSTANTS - EDTINFO WORK +
               AREA'
         DROP  R12
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - REGISTER EQUATES'
         YREGS ,
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - DSECTS - CVT'
         CVT   DSECT=YES,LIST=NO
         PRINT NOGEN
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - DSECTS - PARMLIST FOR UNI+
               VERIFICATION SERVICES (IEFEB4UV)'
         IEFZB4UV ,             PARAMETER LIST FOR IEFEB4UV.
 TITLE 'UNITNAME - EDT DISPLAY - DATA AREAS - DSECTS - UCB'
DSECUCB  DSECT ,
         IEFUCBOB LIST=NO
 TITLE 'UNITNAME'
         END   ,
